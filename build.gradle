apply plugin: 'com.android.application'

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    dexOptions {
        javaMaxHeapSize "2g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['resources']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        test {
            java.srcDirs = ['tests']
        }
    }
    packagingOptions {
        merge "META-INF/DEPENDENCIES"
        merge "META-INF/LICENSE"
        merge "META-INF/NOTICE"
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        debug {
            minifyEnabled true
            signingConfig signingConfigs.debug
        }
        // keep minifyEnabled false above for faster builds; set to 'true'
        // when testing to make sure ProGuard/R8 is not deleting important stuff
        release {
            minifyEnabled true
        }
    }
}

dependencies {

    repositories {
        jcenter()
        google()
        /* Needed for org.apache.sshd debugging
        maven {
            url "https://jitpack.io"
        }
        */
    }

    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.lifecycle:lifecycle-runtime:2.2.0'
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.jakewharton:disklrucache:2.0.2' //For caching album art bitmaps
    implementation 'com.jaredrummler:android-device-names:1.1.9' //To get a human-friendly device name

    implementation 'org.apache.sshd:sshd-core:0.14.0'
    implementation 'org.apache.mina:mina-core:2.0.19' //For some reason, makes sshd-core:0.14.0 work without NIO, which isn't available until Android 8 (api 26)

    //implementation('com.github.bright:slf4android:0.1.6') { transitive = true } // For org.apache.sshd debugging
    implementation 'com.madgag.spongycastle:bcpkix-jdk15on:1.58.0.0' //For SSL certificate generation

    //noinspection AnnotationProcessorOnCompilePath - c.f. https://issuetracker.google.com/issues/140881211
    implementation 'com.jakewharton:butterknife:10.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'

    implementation 'org.atteo.classindex:classindex:3.6'
    annotationProcessor 'org.atteo.classindex:classindex:3.6'

    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.powermock:powermock-core:2.0.0'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.0'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.0'
    testImplementation 'org.mockito:mockito-core:2.23.0'
    testImplementation 'org.skyscreamer:jsonassert:1.3.0'
}
